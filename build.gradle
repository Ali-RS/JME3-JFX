import java.text.SimpleDateFormat;

/////////////////git version logic///////////////
def cmd = "git show -s --pretty=format:%ct_%h"//"git rev-parse --short HEAD"
def proc = cmd.execute()
String[] gitInfos = proc.text.trim().split('_')
String gitRevision = gitInfos[1]
SimpleDateFormat format = new SimpleDateFormat("yyyy-MM-dd_HH:mm:ss")
String gitTimestamp = format.format(new Date(Long.parseLong(gitInfos[0])*1000));

ext.env = System.getenv()
ext.mainBuildVersion = "1";

//jenkins part
if(env.BUILD_NUMBER){
	ext.buildVersion = ext.mainBuildVersion+"."+env.BUILD_NUMBER+"@"+gitTimestamp+"-"+gitRevision
}else{
	ext.buildVersion = ext.mainBuildVersion+".dev@"+gitTimestamp+"-"+gitRevision
}
print("Build is based on " + ext.buildVersion);
/////////////////git version logic///////////////


apply plugin: 'java'
sourceCompatibility=JavaVersion.VERSION_1_8
compileJava.options.encoding = 'UTF-8'


apply plugin: 'maven'
apply plugin: 'eclipse'

version = "1."+gitTimestamp+"."+ gitRevision
group = "com.jme3x"


repositories {
	mavenCentral()

	maven { url "http://updates.jmonkeyengine.org/maven/"}
}

configurations {
	provided
}

sourceSets {
	main { compileClasspath += configurations.provided }
	test { compileClasspath += configurations.provided }
}
sourceSets.main.resources.srcDir 'src/main/java'

def v_slf4j = '1.7.7'
def v_jme3 = '3.0.10'
dependencies {
	compile group: 'org.jfxtras', name: 'jfxtras-window', version:'8.0-r2'
	compile "org.slf4j:slf4j-api:${v_slf4j}"
	provided "com.jme3:jme3-core:${v_jme3}"
	provided "com.jme3:jme3-desktop:${v_jme3}"
	provided "com.jme3:jme3-lwjgl:${v_jme3}"
	testRuntime "org.slf4j:slf4j-jdk14:${v_slf4j}"
}

task packageSources(type: Jar) {
	from sourceSets.main.allSource
	classifier = 'sources'
}

artifacts {
	archives(packageSources) {
		type = 'jar'
	}
}

eclipse {
	classpath {
		plusConfigurations += [configurations.provided]
		noExportConfigurations += [configurations.provided]
	}
}

buildscript {
	repositories {
		jcenter()
	}
	dependencies {
		classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.5'
	}
}

apply plugin: 'com.jfrog.bintray'

bintray {
	user = targetRepository_user
	key = targetRepository_password

	configurations = ['archives']
	//publications = ['maven'] //When uploading Maven-based publication files
	dryRun = false //Whether to run this as dry-run, without deploying
	publish = true //If version should be auto published after an upload
	pkg {
		repo = 'VGS-OSS-Releases'
		name = project.name
		desc = "JFX Gui bridge for JME with usefull utilities for common usecases."
		websiteUrl = "https://github.com/empirephoenix/JME3-JFX"
		issueTrackerUrl = "https://github.com/empirephoenix/JME3-JFX/issues"
		vcsUrl = "https://github.com/empirephoenix/JME3-JFX.git"
		licenses = ['BSD 3-Clause']
		labels = ['jmonkeyengine', 'gui']
		publicDownloadNumbers = true
	}
}
