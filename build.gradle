import java.text.SimpleDateFormat;

/////////////////git version logic///////////////
def cmd = "git rev-parse --short HEAD"
def proc = cmd.execute()
String gitRevision = proc.text.trim()
SimpleDateFormat format = new SimpleDateFormat("yyyyMMddHHmmss")
String gitTimestamp = format.format(new Date());
/////////////////git version logic///////////////


apply plugin: 'java'
sourceCompatibility=JavaVersion.VERSION_1_8
compileJava.options.encoding = 'UTF-8'


apply plugin: 'maven'
apply plugin: 'eclipse'

version = "1."+gitTimestamp+"."+ gitRevision
group = "com.jme3x"


repositories {
	mavenCentral()

	maven { url "https://oss.sonatype.org/content/groups/public"}
}

configurations {
	provided
}

sourceSets {
	main { compileClasspath += configurations.provided }
	test { compileClasspath += configurations.provided }
}
sourceSets.main.resources.srcDir 'src/main/java'

def v_slf4j = '1.7.7'

dependencies {
	compile group: 'org.jfxtras', name: 'jfxtras-window', version:'8.0-r2-SNAPSHOT'
	compile "org.slf4j:slf4j-api:${v_slf4j}"
	provided files('providedlibs/jme3-core.jar','providedlibs/jme3-desktop.jar','providedlibs/jme3-lwjgl.jar','providedlibs/lwjgl.jar')
	testRuntime "org.slf4j:slf4j-jdk14:${v_slf4j}"
}

task packageSources(type: Jar) {
	from sourceSets.main.allSource
	classifier = 'sources'
}

artifacts {
	archives(packageSources) {
		type = 'jar'
	}
}

uploadArchives {
	repositories {
		mavenDeployer {
			repository(url: "$targetRepository/releases") {
				 authentication(userName: targetRepository_user, password: targetRepository_password)
			}
		 }
	}
}

eclipse {
	classpath {
		plusConfigurations += [configurations.provided]
		noExportConfigurations += [configurations.provided]
	}
}
